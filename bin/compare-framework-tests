#!/usr/bin/env perl
use strict;
use warnings;
use 5.010;
use Data::Dumper;
use FindBin '$Bin';
use lib "$Bin/../lib/perl5";
use YAML::XS qw/ LoadFile DumpFile /;
use Encode;
use Getopt::Long;
Getopt::Long::Configure ("bundling");
use File::Basename qw/ basename /;
use IO::All;
use List::Util qw/ min /;
use YAML::PP;
use YAML::Matrix qw/
    minimal_events
    hsyaml_event_to_event
    generate_expected_output
    load_csv gather_tags
/;

my $docker_user = $ENV{DOCKER_USER} || 'yamlio';
my $not_implemented = LoadFile("ni.yaml");
my $yp = YAML::PP->new( schema => [qw/ + Merge /] );
my $list = $yp->load_file("$Bin/../list.yaml");

GetOptions(
    "help|h" => \my $help,
    "list|l" => \my $listviews,
    "all|a" => \my $all,
    "verbose|v+" => \my $verbose,
    "data-dir=s" => \my $datadir,
    "matrix-dir=s" => \my $matrixdir,
    "view|f=s" => \my $view,
);
$verbose ||= 0;
$datadir //= "$Bin/../yaml-test-suite/data";
$matrixdir //= "$Bin/../matrix";

my %skip = (
    'perl-pplibyaml' => { event => 1 },
);

my %views;
for my $lib (sort keys $list->{libraries}->%*) {
    my $conf = $list->{libraries}->{ $lib };
    my $runtime = $conf->{runtime};
    my $tests = $conf->{tests};
    for my $test (@$tests) {
        next if $skip{ $lib }->{ $test };
        next if $test =~ m/^(yaml)$/;
        $views{ "$lib.$test" } = {
            runtime => "alpine-runtime-$runtime",
        };
    }
}
my @views = sort keys %views;

my $id2tag = gather_tags("$datadir/tags");

if ($help) {
    usage();
}
elsif ($listviews) {
    say for @views;
}
elsif ($all) {
    for my $fw (@views) {
        compare_tests(
            view => $fw,
            data => $datadir,
        );
    }
}
elsif ($view) {
    compare_tests(
        view => $view,
        data => $datadir,
    );
}
else {
    usage();
}

sub compare_tests {
    my %args = @_;
    my $view = $args{view};
    say "-" x 80;
    say "Running $view";
    my $data = $args{data};
    my $config = $views{ $view }
        or die "View $view not supported";

    my ($sum_ok, $sum_diff, $sum_na, $sum_err, $sum_naerror, $sum_ni) = (0) x 6;
    my $outfile = "$matrixdir/data/$view-out.yaml";
    if ($views{ $view }) {
    }
    else {
        die "view $view not supported";
    }
    say "Comparing results...";

    my $test_info = LoadFile($outfile);
    my $tests = $test_info->{tests};
    for my $id (sort keys %$tests) {
        my $out = $tests->{ $id };
        my $idf = $id =~ s{:}{/}r;
        my ($title) = io->file("$data/$idf/===")->chomp->slurp;
        $tests->{ $id }->{title} = $title;
        my $tag_list = $id2tag->{ $id };
        my %tags = map { $_ => 1 } @$tag_list;
        if (-f "$data/$idf/error") {
            $out->{invalid} = 1;
            my $error = $out->{error};
            $out->{invalid_correct} = 0;
            $out->{invalid_incorrect} = 0;
            if ($error) {
                $out->{invalid_correct} = 1;
            }
            else {
                $out->{invalid_incorrect} = 1;
            }
            next;
        }
        print "Comparing results for $id...\r";
        my $error = $out->{error};
        my $stdout = delete $out->{stdout} // '';
        my $stderr = delete $out->{stderr} // '';

        my @got = split m/\n/, $stdout;
        my @test_events = io->file("$data/$idf/test.event")->encoding('utf-8')->chomp->slurp;
        my $got_data = $stdout . $stderr;
        $tests->{ $id }->{got_data} = $got_data;

        if ($error) {
            my $msg = $got_data;
            $tests->{ $id }->{msg} = $msg;
        }

        if ($view =~ m/\.json$/ and not -f "$data/$idf/in.json") {
            $tests->{ $id }->{na} = 1;
            $sum_na++;
            if ($error) {
                $tests->{ $id }->{naerror} = 1;
                $sum_naerror++;
                delete $out->{error};
            }
            next;
        }
        if ($error) {
            my $ignore = $not_implemented->{ $view } || [];
            if (@$ignore) {
                my @t = grep { $tags{ $_ } } @$ignore;
                if (@t) {
                    $tests->{ $id }->{ni} = 1;
                    delete $out->{error};
                    $sum_ni++;
                    next;
                }
            }

        }

        if ($error) {
            $sum_err++;
            next;
        }

        if ($view =~ m/\.json$/) {
            my $got_json = $stdout;
            $tests->{ $id }->{got_json} = $got_json;
            my $tmpfile = "/tmp/yaml-test-matrix-tmp-$id.json";
            open my $fh, ">", $tmpfile or die $!;
            print $fh encode_utf8 $got_json;
            close $fh;
            my $got_json2 = qx{jq --sort-keys . < $tmpfile 2>&1};
            unlink $tmpfile;
            $got_json = decode_utf8 $got_json2;

            my $exp_json = io
                ->file("$matrixdir/expected/$id/in.json")
                ->encoding('utf-8')->chomp->slurp;
            $tests->{ $id }->{got_json} = $got_json;
            my $got_json_lines = [split m/\n/, $got_json ];
            my $exp_json_lines = [split m/\n/, $exp_json ];
            my $ok = 1;
            my $diff_line;
            for my $i (0 .. $#$got_json_lines) {
                if (($got_json_lines->[ $i ] // '') ne ($exp_json_lines->[ $i ] // '')) {
                    $ok = 0;
                    $diff_line = $i;
                    last;
                }
            }
            if ($ok and @$got_json_lines != @$exp_json_lines) {
                $ok = 0;
                $diff_line = min(scalar @$got_json_lines, scalar @$exp_json_lines);
            }
            if ($ok) {
                $tests->{ $id }->{json_ok} = 1;
                $sum_ok++;
            }
            else {
                $tests->{ $id }->{json_diff} = 1;
                $tests->{ $id }->{diff_line} = $diff_line;
                $sum_diff++;
            }
        }
        elsif ($view =~ m/\.(perl|p6|py|raku|yeast)$/) {
            $tests->{ $id }->{na} = 1;
            $sum_na++;
        }
        elsif ($view =~ m/\.event$/) {
            my @events = @got;
            my @orig_events = @test_events;
            if ($view eq "cpp-yamlcpp.event") {
                @test_events = io
                    ->file("$matrixdir/expected/$id/minimal.cpp.event")
                    ->encoding('utf-8')->chomp->slurp;
                $tests->{ $id }->{minimal_events} = \@test_events;
                $tests->{ $id }->{transformed_events} = \@events;
            }
            elsif ($view eq "cpp-rapidyaml.event") {
                @test_events = io
                    ->file("$matrixdir/expected/$id/minimal.rapid.event")
                    ->encoding('utf-8')->chomp->slurp;
                $tests->{ $id }->{minimal_events} = \@test_events;
                $tests->{ $id }->{transformed_events} = \@events;
            }
            elsif ($view eq "rust-yamlrust.event") {
                @test_events = io
                    ->file("$matrixdir/expected/$id/minimal.rust.event")
                    ->encoding('utf-8')->chomp->slurp;
                $tests->{ $id }->{minimal_events} = \@test_events;
                $tests->{ $id }->{transformed_events} = \@events;
            }
            # These don't output flow collection information yet
            elsif ($view eq "dotnet-yamldotnet.event"
                or $view eq "nim-nimyaml.event"
                or $view eq "hs-hsyaml.event"
                ) {
                @test_events = io
                    ->file("$matrixdir/expected/$id/minimal.flow.event")
                    ->encoding('utf-8')->chomp->slurp;
                $tests->{ $id }->{minimal_events} = \@test_events;
                if ($view eq "hs-hsyaml.event") {
                    @events = hsyaml_event_to_event(@events);
                    $tests->{ $id }->{transformed_events} = \@events;
                }
            }
            my $ok = 1;
            my $diff_line;
            for my $i (0 .. $#events) {
                my $event = $events[ $i ];
                my $exp = $test_events[ $i ];
                if ($event ne $exp) {
                    $verbose and
                        warn "$id line ${\($i + 1)}: <$event> ne <$exp>\n";
                    $ok = 0;
                    $diff_line = $i;
                    last;
                }
            }
            if ($ok and @events != @test_events) {
                $ok = 0;
                $diff_line = min(scalar @events, scalar @test_events);
            }
            if ($ok) {
                $tests->{ $id }->{ok} = 1;
                $sum_ok++;
            }
            else {
                if ($verbose >= 2) {
                    warn __PACKAGE__.':'.__LINE__.$".Data::Dumper->Dump([\@events], ['events']);
                    warn __PACKAGE__.':'.__LINE__.$".Data::Dumper->Dump([\@test_events], ['test_events']);
                    warn __PACKAGE__.':'.__LINE__.$".Data::Dumper->Dump([\@orig_events], ['orig_events']);
                }
                $tests->{ $id }->{diff} = 1;
                $tests->{ $id }->{diff_line} = $diff_line;
                $sum_diff++;
                $tests->{ $id }->{got_events} = \@got;
            }
        }
    }
    print "Done                         \n";
    printf "ok: %d diff: %d n/a: %d error: %d n/a+error: %d n/i: %d\n",
        $sum_ok, $sum_diff, $sum_na, $sum_err, $sum_naerror, $sum_ni;

    say "Saving tests into $matrixdir/data/$view.yaml";
    DumpFile("$matrixdir/data/$view.yaml", $test_info);

}

sub usage {
    say <<"EOM";

Usage:

Run in docker:
    $0 --view <view>

Specify a script:
    $0 --view <view> --path /path/to/view

Options:
--view, -f      view (see -l)
--list, -l      List supported views
--verbose, -v   Increase verbosity:
                -v  Print the first differing lines of event output
                -vv Dump events and expected events
--data-dir      /path/to/yaml-test-suite/data
--all, -a       Run all views
EOM
    exit 1;
}


